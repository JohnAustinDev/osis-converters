#!/usr/bin/perl
# This file is part of "osis-converters".
# 
# Copyright 2012 John Austin (gpl.programs.info@gmail.com)
#     
# "osis-converters" is free software: you can redistribute it and/or 
# modify it under the terms of the GNU General Public License as 
# published by the Free Software Foundation, either version 2 of 
# the License, or (at your option) any later version.
# 
# "osis-converters" is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with "osis-converters".  If not, see 
# <http://www.gnu.org/licenses/>.

# usage: osis2sword [Project_Directory]

# Run this script to create raw and zipped SWORD modules from an 
# osis.xml file and a config.conf file located in the Project_Directory.

# OSIS wiki: http://www.crosswire.org/wiki/OSIS_Bibles
# CONF wiki: http://www.crosswire.org/wiki/DevTools:conf_Files

use strict; use File::Spec; our $SCRIPT = File::Spec->rel2abs(__FILE__); our $SCRD = $SCRIPT; $SCRD =~ s/([\\\/][^\\\/]+){2}$//; require "$SCRD/lib/common/bootstrap.pm"; &init(shift, shift);

our ($WRITELAYER, $APPENDLAYER, $READLAYER);
our ($SCRD, $MOD, $INPD, $MAINMOD, $MAININPD, $DICTMOD, $DICTINPD, $TMPDIR);
our ($INOSIS, $SWORD_BIN, $MODULETOOLS_BIN, $XPC, $XML_PARSER, 
    $UPPERCASE_DICTIONARY_KEYS, %CONV_PUB_SETS);
    
my $swtmp = &outdir() . '/sword';
if (-e "$swtmp") {remove_tree("$swtmp");}
make_path("$swtmp");

&runAnyUserScriptsAt("sword/preprocess", \$INOSIS);

my $Sconf = &getSwordConf($INOSIS);

my $SModDrv = $Sconf->{"$MOD+ModDrv"};
my $SModPath = &dataPath2RealPath($Sconf->{"$MOD+DataPath"});
my $SModVsys = $Sconf->{"$MOD+Versification"};
if (! -e "$swtmp/$SModPath") {make_path("$swtmp/$SModPath");}

# Prepare osis-converters OSIS for SWORD import. DICT modules are
# run after their MAIN module, so during MAIN processing, DICTMOD_URI is
# empty, in case it does not yet exist.
my %params = (
  'conversion' => join(' ', 'sword', @{$CONV_PUB_SETS{'sword'}}),
  'MAINMOD_URI' => &getModuleOsisFile($MAINMOD), 
  'DICTMOD_URI' => ($DICTMOD && $MOD eq $DICTMOD ? &getModuleOsisFile($DICTMOD):''),
);
&logXSLT(&runScript("$SCRD/lib/sword.xsl", \$INOSIS, \%params, 3));

&usePngIfAvailable(\$INOSIS);

&useSwordImgIfAvailable(\$INOSIS);

if ($SModDrv =~ /GenBook/) {
  &checkChildrensBibleStructure($INOSIS);
  &runScript("$SCRD/lib/genbook/sword.xsl", \$INOSIS);
}

# Apply CrossWire ModuleTools osis2sword.xsl
my $OSIS_OR_TEI = $INOSIS; # could be OSIS or TEI after the next step
my $typePreProcess = ($SModDrv =~ /Text/ ? 'osis2sword.xsl':($SModDrv =~ /LD/ ? 'osis2tei.xsl':''));
if ($typePreProcess) {&runScript($MODULETOOLS_BIN.$typePreProcess, \$OSIS_OR_TEI);}

# Uppercasing must be done by Perl to use uc2()
if ($UPPERCASE_DICTIONARY_KEYS) {&upperCaseKeys(\$OSIS_OR_TEI);}

# Copy images and set Feature conf entry
my $imgsAP = &copyReferencedImages(\$OSIS_OR_TEI, $INPD, "$swtmp/$SModPath");
if (@{$imgsAP}) {
  $Sconf->{"$MOD+Feature"} = ( 
    $Sconf->{"$MOD+Feature"} ? $Sconf->{"$MOD+Feature"} . '<nx/>' : '' 
  ) . 'Images';
}

# Validate osisRef and src attribtues
my %params;
if ($SModDrv =~ /LD/) {
  # find the final OSIS file that was used to build the MAIN SWORD module
  my $mainmod = &getModuleOsisFile($MAINMOD);
  $mainmod =~ s/\/[^\/]+$//;
  $mainmod .= '/tmp/osis2sword';
  if (opendir(TF, $mainmod)) {
    my @fs = readdir(TF);
    closedir(TF);
    my $n = 0; my $name;
    foreach my $f (@fs) {
      if ($f =~ /^(\d+)/ && int($1) > $n) {
        $n = int($1); $name = $f;
      }
    }
    $mainmod = "$mainmod/$name";
    if (-e $mainmod) {
      %params = (
        'MAINMOD' => $MAINMOD, 
        'MAINMOD_URI' => $mainmod, 
        'DICTMOD_URI' => $OSIS_OR_TEI
      );
    }
    else {
      &Error("Could not locate SWORD main module.", 
      "The main SWORD module must be created before the dict SWORD module.");
    }
  }
  else {
    &Error("Main module not found, unable to run checkrefsSWORD.xsl");
  }
}
else {
  %params = (
    'MAINMOD' => $MAINMOD, 
    'MAINMOD_URI' => $OSIS_OR_TEI,
    'DICTMOD_URI' => ''
  );
}
if (%params) {
  $params{'moduleFiles'} = join('|', @{$imgsAP});
  my $msg = &runXSLT("$SCRD/lib/checkrefsSWORD.xsl", $OSIS_OR_TEI, undef, \%params);
  my $err = () = $msg =~ /ERROR/g;
  &Report("Found $err problem(s) with links of $MAINMOD".($DICTMOD ? " and $DICTMOD":'').".\n");
}

# Check length of Bible introduction
if ($SModDrv =~ /Text$/) {
  my $introlen = 0;
  foreach my $i (@{$XPC->findnodes('(//osis:osisText/node() | //osis:div[@type="bookGroup"]/node())
  [not(self::osis:header)]
  [not(self::osis:div[@type="bookGroup"])]
  [not(self::osis:div[@type="book"])]
  [not(preceding::osis:div[@type="book"])]', $XML_PARSER->parse_file($OSIS_OR_TEI))}) {
    $introlen += length($i->toString());
  }
  if ($introlen > 64000) {
    &Error(
"Bible introduction is too long (".$introlen." > 64000)", 
"Shorten the introduction by moving parts of it into the DICT 
module and replacing it with links. Long entries are difficult to scroll 
through in some media and should be avoided. But if there is very good 
reason to require an exceedingly long introduction, the SWORD Text4 
module type might be used, which supports larger entries.");
  }
}

# Write the SWORD module
if ($SModDrv =~ /Text/) {
  &Log("\n--- CREATING $MOD SWORD MODULE ($SModVsys)\n");
  
  &shell( &escfile($SWORD_BIN."osis2mod") . ' ' . 
          &escfile("$swtmp/$SModPath") . ' ' . 
          &escfile($OSIS_OR_TEI) . ' ' . 
          ($SModDrv =~ /zText/ ? ' -z z' : '') . ' ' .
          "-v " . $SModVsys . 
          ($SModDrv =~ /Text4/ ? ' -s 4' : ''), -1);
  
}
elsif ($SModDrv =~ /^RawGenBook$/) {
	&Log("\n--- CREATING $MOD RawGenBook SWORD MODULE ($SModVsys)\n");
  
	chdir("$swtmp/$SModPath");
  &shell(&escfile($SWORD_BIN."xml2gbs")." $OSIS_OR_TEI ".lc($MOD), -1);
	chdir($SCRD);
  
}
elsif ($SModDrv =~ /LD/) {
  &Log("\n--- CREATING $MOD Dictionary TEI SWORD MODULE ($SModVsys)\n");
  
  &shell(&escfile($SWORD_BIN."tei2mod")." ".&escfile("$swtmp/$SModPath")." ".&escfile($OSIS_OR_TEI)." -s ".($SModDrv eq "RawLD" ? "2":"4"), -1);
  # tei2mod creates module files called "dict" which are non-standard, so fix
  opendir(MODF, "$swtmp/$SModPath");
  my @mf = readdir(MODF);
  closedir(MODF);
  foreach my $m (@mf) {
  if ($m !~ /^dict\.(.*?)$/) {next;}
    rename("$swtmp/$SModPath/$m", "$swtmp/$SModPath/".lc($MOD).".$1");
  }
  
}
else {
	&ErrorBug("Unhandled module type \"$SModDrv\"; only the following are supported: Bible, Dictionary or General-Book", 1);
}

# Copy PreferredCSSXHTML css and set PreferredCSSXHTML conf entry
my $cssfile = &getDefaultFile( ($MOD eq $DICTMOD ? 'DICTMOD' : '') .
    "/sword/css/module.css", -1);
if ($cssfile) {
  &Log("\n--- COPYING PreferredCSSXHTML \"$cssfile\"\n");
  copy($cssfile, "$swtmp/$SModPath");
}

# Set InstallSize conf entry
{
  my $installSize = 0;             
  find(sub { $installSize += -s if -f $_ }, "$swtmp/$SModPath");
  $Sconf->{"$MOD+InstallSize"} = $installSize;
}

# Write the SWORD config.conf file
my $SwordConfFile = "$swtmp/mods.d/".lc($MOD).".conf";
if (! -e "$swtmp/mods.d") {mkdir "$swtmp/mods.d";}
&writeConf($SwordConfFile, $Sconf);
my $Version = $Sconf->{"$MOD+Version"};
&zipModule(&outdir()."/$MOD-$Version.zip", $swtmp);

# Copy config.conf for CVS
&copy(&escfile($SwordConfFile), &outdir()."/config.conf");

&timer('stop');

########################################################################
########################################################################

sub usePngIfAvailable {
  my $osisP = shift;
  
  my $xml = $XML_PARSER->parse_file($$osisP);
  
  &Note("Using png images if available...");
  
  # use png images if available
  my @jpgs = $XPC->findnodes('//osis:figure[contains(@src, ".jpg") or contains(@src, ".JPG")]', $xml);
  foreach my $jpg (@jpgs) {
    my $src = $jpg->getAttribute('src');
    $src =~ /^(.*)(\.jpg)$/i; my $pnm = $1; my $ext = $2;
    if (-e "$INPD/$pnm.png") {
      $jpg->setAttribute('src', "$pnm.png");
      &Note("Changing jpg image to png: ".$jpg->getAttribute('src'));
    }
  }
  
  &writeXMLFile($xml, $osisP);
}

sub useSwordImgIfAvailable {
  my $osisP = shift;
  
  &Note("Using '_sword' images if available...");
  
  my $xml = $XML_PARSER->parse_file($$osisP);
  
  # use Sword specific images if available
  my @figs = $XPC->findnodes('//osis:figure', $xml);
  foreach my $fig (@figs) {
    my $src = $fig->getAttribute('src');
    my $swsrc = $src; $swsrc =~ s/(?=\.([^.]+)$)/_sword/;
    if ($src !~ /_sword/ && -e "$INPD/$swsrc") {
      $fig->setAttribute('src', $swsrc);
      &Note("Changing $src image to: ".$fig->getAttribute('src'));
    }
  }
  
  &writeXMLFile($xml, $osisP);
}

# uppercase dictionary keys were necessary to avoid requiring ICU in SWORD.
# XSLT was not used to do this because a custom uc2() Perl function is needed.
sub upperCaseKeys {
  my $osis_or_teiP = shift;
  
  my $xml = $XML_PARSER->parse_file($$osis_or_teiP);
  if ($MOD eq $DICTMOD) {
    foreach my $keyword ($XPC->findnodes('//*[local-name()="entryFree"]/@n', $xml)) {
      $keyword->setValue(&uc2($keyword->getValue()));
    }
    # These DICT note osisRefs are unnecessary so remove rather than change them
    foreach my $osisRef ($XPC->findnodes('//*[local-name()="note"]/@osisRef', $xml)) {
      $osisRef->unbindNode();
    }
  }
  my @dictrefs = $XPC->findnodes('//*[local-name()="reference"][starts-with(@type, "x-gloss")]/@osisRef', $xml);
  foreach my $dr (@dictrefs) {
    my @new;
    foreach my $dictref (split(/\s+/, $dr->getValue())) {
      my $mod; my $e = &osisRef2Entry($dictref, \$mod);
      push(@new, &entry2osisRef($mod, &uc2($e)));
    }
    $dr->setValue(join(' ', @new));
  }

  &writeXMLFile($xml, $osis_or_teiP);
}

sub dataPath2RealPath {
  my $datapath = shift;

  $datapath =~ s/([\/\\][^\/\\]+)\s*$//; # remove any file name at end
  $datapath =~ s/[\\\/]\s*$//; # remove ending slash
  $datapath =~ s/^[\s\.]*[\\\/]//; # normalize beginning of path
  return $datapath;
}


1;
